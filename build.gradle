buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'nebula.release' version '15.2.0'
    id "com.github.spotbugs" version "4.6.0"
}

group = 'com.github.bijukunjummen'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

ext {
    jacksonBom = "2.11.3"
    junitVersion = "5.4.2"
    assertJVersion = "3.16.1"
    testContainersVersion = "1.15.2"
    jsonAssertVersion = "1.5.0"
    awsSdkVersion = "2.16.15"
    reactorVersion = "3.4.2"
    mockitoVersion = "3.3.3"
    jakartaVersion = "1.3.5"
}
repositories {
    mavenCentral()
}
dependencies {
    api("com.fasterxml.jackson.core:jackson-core:${jacksonBom}")
    api("com.fasterxml.jackson.core:jackson-databind:${jacksonBom}")
    api("software.amazon.awssdk:sns:${awsSdkVersion}")
    api("software.amazon.awssdk:sqs:${awsSdkVersion}")
    api("io.projectreactor:reactor-core:${reactorVersion}")
    api("io.projectreactor.addons:reactor-extra:${reactorVersion}")
    api("jakarta.annotation:jakarta.annotation-api:${jakartaVersion}")
    testImplementation("io.projectreactor:reactor-test:${reactorVersion}")
    testImplementation("org.assertj:assertj-core:$assertJVersion")
    testImplementation("org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:$junitVersion")
    testImplementation("org.testcontainers:testcontainers:${testContainersVersion}")
    testImplementation("org.testcontainers:localstack:${testContainersVersion}")
    testImplementation("com.amazonaws:aws-java-sdk-sqs:1.11.930") // for localstack
    testImplementation('org.awaitility:awaitility:4.0.3')
    testImplementation("org.skyscreamer:jsonassert:${jsonAssertVersion}")
    testImplementation("org.mockito:mockito-core:${mockitoVersion}")
    testImplementation("org.mockito:mockito-junit-jupiter:${mockitoVersion}")
    testImplementation("org.slf4j:slf4j-simple:1.7.30")
}

test {
    useJUnitPlatform {
        excludeTags "integration"
    }
}

task integration(type: Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}
ext.isReleaseVersion = !version.toString().endsWith("-SNAPSHOT")

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

tasks.snapshot {
    finalizedBy tasks.publish
}

tasks.candidate {
    finalizedBy tasks.publish
}

tasks.final {
    finalizedBy tasks.publish
}

publishing {
    repositories {
        maven {
            def snapshotUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            def releaseUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

            url = isReleaseVersion ? releaseUrl : snapshotUrl
            credentials {
                username ossrhUsername
                password ossrhPassword
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'aws-sdk2-reactive-messaging'
                description = 'Generate a reactive stream from AWS SNS/SQS'
                url = 'https://github.com/bijukunjummen/aws-sdk2-reactive-messaging.git'
                inceptionYear = '2021'

                packaging = 'jar'
                groupId = project.group
                artifactId = project.name
                version = project.version

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                scm {
                    connection = 'https://github.com/bijukunjummen/aws-sdk2-reactive-messaging.git'
                    url = 'https://github.com/bijukunjummen/aws-sdk2-reactive-messaging.git'

                }
                developers {
                    developer {
                        name = 'Biju Kunjummen'
                        id = 'bijukunjummen'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}


